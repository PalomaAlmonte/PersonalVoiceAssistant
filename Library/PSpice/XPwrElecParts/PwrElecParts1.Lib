* PSpice Model Editor - Version 9.2
*

****************************************************************** 
*$
* PSpice Model Editor - Version 9.2
* Mixed a/d model for Cmos version of 555.
*
* Notes from Cadence: This model is not supported by the Basics+ package.
* The Delay Line model that this model uses is not supported by the Basics+
* package.
*
.subckt LM555 1 2 3 4 5 6 7 8
+ params:maxfreq=3e6
  r1 8     5    13k
  r2 5     botm 13k
  r3 botm  0    13k
  m1 7 qb 0 0 nchan l=2u w=1000u
  otop 6     5  cmp dgtlnet=r io_std
  obot botm  2  cmp dgtlnet=s io_std
  ud1 dlyline 8 1 s sd dlymod io_std
  ud2 dlyline 8 1 r rd dlymod io_std
  u1 srff(1) 8 1 strt 4 hi sd rd 3 qb t_srff io_555
  uhigh stim(1,1) 8 1  hi io_stm 0s 1
  ustrt stim(1,1) 8 1  strt io_stm 0s 0 1ns 1
.model nchan nmos cgbo=1p cgdo=1p cgso=1p
.model dlymod udly(dlymn={.5/maxfreq}
+ dlyty={.5/maxfreq}
+ dlymx={0.5/maxfreq})
.model cmp doutput(
+ s0name=0 s0vlo=-500 s0vhi=0
+ s1name=1 s1vlo=   0 s1vhi=500)
.model io_555 uio (
+	drvh=96.4	drvl=104
+	atod1="atod_555"	atod2="atod_555"
+	atod3="atod_555"	atod4="atod_555"
+	dtoa1="dtoa_555"	dtoa2="dtoa_555"
+	dtoa3="dtoa_555"	dtoa4="dtoa_555")
.model t_srff ugff (tppcqlhty=120ns)
.ends
*$
***********************************************************
* IRFD9110
.model IRFD9110	PMOS( Kp=0.67 Vto=-3)
*
************************************************************
* IRFD110
.model IRFD110 NMOS( Kp=0.67 Vto=3)
*		
************************************************************
* source PWM_SAW
.SUBCKT PWM_SAW c s  
E_ABM21         s 0 VALUE { IF(V(c)-V(N00011)>0, 1, 0)    }
V_V1         N00011 0  
+PULSE 0 1 0 9.98us 0.01u 0.01us (10us)
.ENDS
*
*
.model DZBV3V D(Is=1.085f Rs=.7945 Ikf=0 N=1 Xti=3 Eg=1.11 Cjo=157p M=.2966
+               Vj=.75 Fc=.5 Isr=2.811n Nr=2 Bv=3 Ibv=.37157 Nbv=.64726
+               Ibvl=1m Nbvl=6.5761 Tbv1=267.86u)
*               Motorola        pid=1N4734      case=DO-41
*               89-9-19 gjg
*               Vz = 3V @ 45mA, Zz = 40 @ 1mA, Zz = 4.5 @ 5mA, Zz = 1.9 @ 20mA
*$
***********************************************************
**********************************************************
*
* Linear Transformer with Center-tapped Secondary
.subckt XFRM_CT-SEC 1 3 4 5 6 Params: 
+ Lp_turns=200 
+ Ls1_turns=100 
+ Ls2_turns=100 
+ Coupling=.99 
+ Rp_value=.25 
+ Rs_value=.25

Lp1 7 2 {Lp_turns/2}
Lp2 2 8 {Lp_turns/2}
Ls1 9 5 {Ls1_turns}
Ls2 5 10 {Ls2_turns}
Rp1 1 7 {Rp_value/2}
Rp2 8 3 {Rp_value/2}
Rs1 9 4 {Rs_value/2}     
Rs2 10 6 {Rs_value/2}
K1 Lp1 Lp2 Ls1 Ls2 {coupling}
.ends
*$
*$
.SUBCKT 2N6027A 1 2 3
**************************************
*      Model Generated by CZ LAB     *
*           April 20, 2001           *
*   Copyright(c) On Semiconductor    *
*         All Rights Reserved        *
*Commercial Use or Resale Restricted *
**************************************
*Programable Unijunction Transistor
*MODEL FORMAT: PSpice
*       anode  gate  cathode
*node:    1      2      3
Q1 2 4 3 NMOD

Q2 4 2 1 PMOD

.MODEL NMOD NPN(IS=5E-15 VAF=100 IKF=0.3 ISE=1.85E-12
+ NE=1.45 RE=0.15 RC=0.15 CJE=7E-10 TF=0.6E-8
+ CJC=2.2E-10 TR=4.76E-8 XTB=3)

.MODEL PMOD PNP(IS=2E-15 VAF=100 IKF=0.3 ISE=1.90E-12
+ NE=1.5 RE=0.15 RC=0.15 CJE=7E-10 TF=1.6E-8
+ CJC=2.2E-10 TR=5.1E-8 XTB=3)

.ENDS
*$

*
.subckt BTA06-600BW A K G
X1 A K G Triac_ST params:
+ Vdrm=600v     
+ Igt=50ma
+ Ih=50ma        
+ Rt=0.06
+ Standard=0
* 2008 / ST / Rev 1
.ends
*$
.subckt BTW67-600 A K G
X1 A K G SCR_ST params:
+ Vdrm=600v     
+ Igt=80ma
+ Ih=150ma        
+ Rt=0.0085
* 2008 / ST / Rev 0
.ends
*$
.subckt DB3 DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Vbo=13.6V
+Delta_V=19V
+Ibo=10uA
* 2008 / ST / Rev 0
.ends
*$
*